import org.asciidoctor.gradle.jvm.AsciidoctorTask

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.asciidoctor.jvm.convert' version '4.0.5'
}

configurations {
    asciidoctorExtensions
}

dependencies {
    // 공통 모듈 의존성 추가
    implementation project(':uppick-common')

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'com.h2database:h2'

    // QueryDSL: JPA 및 APT (jakarta 플러그인 사용)
    implementation "com.querydsl:querydsl-jpa:${querydslVersion}:jakarta"
    annotationProcessor "com.querydsl:querydsl-apt:${querydslVersion}:jakarta"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    //Rest Docs
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    asciidoctorExtensions 'org.springframework.restdocs:spring-restdocs-asciidoctor'

    // AWS S3
    implementation 'io.awspring.cloud:spring-cloud-aws-starter-s3'

    // Open Feign
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // Redisson
    implementation 'org.redisson:redisson-spring-boot-starter:3.52.0'

    // Prometheus
    implementation 'io.micrometer:micrometer-registry-prometheus'

    // OpenSearch
    implementation 'org.opensearch.client:spring-data-opensearch-starter:2.0.3'
    implementation 'org.opensearch.client:opensearch-java:3.3.0'
    implementation 'software.amazon.awssdk:apache-client'

    // RabbitMQ (Spring AMQP)
    implementation 'org.springframework.boot:spring-boot-starter-amqp'

    // flyway
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'

    // AWS Secret Manager Spring Boot Starter
    implementation 'io.awspring.cloud:spring-cloud-aws-starter-secrets-manager'
}

// REST Docs 스니펫 디렉토리 설정
ext {
    snippetsDir = file('build/generated-snippets')
}

// restdocs-index.gradle을 먼저 적용하여 generateRestDocsIndex task 생성
apply from: rootProject.file('gradle/scripts/restdocs-index.gradle')

tasks.named('test', Test) {
    outputs.dir snippetsDir
    useJUnitPlatform()
    finalizedBy 'asciidoctor'
}

tasks.named('asciidoctor', AsciidoctorTask) {
    configurations 'asciidoctorExtensions'
    inputs.dir snippetsDir
    baseDirFollowsSourceDir()
    attributes "snippets": snippetsDir
    dependsOn tasks.named('generateRestDocsIndex')

    // snippets가 있을 때만 실행
    onlyIf {
        snippetsDir.exists()
    }
}
